plugins {
    id 'fabric-loom' version '1.10-SNAPSHOT'
    id 'maven-publish'
    id 'net.kyori.indra.publishing.sonatype' version '3.1.3'
    id 'net.kyori.indra.publishing' version '3.1.3'
}

repositories {
    maven { url 'https://maven.fabricmc.net/' }
}

def minecraftVersion = '1.21.6'
def yarnBuild = 1
def loaderVersion = '0.16.14'
def fabricApiVersion = '0.127.0+1.21.6'

group = 'me.lucko'
version = '0.4.1-SNAPSHOT'
description = 'A simple permissions API for Fabric'

dependencies {
    minecraft "com.mojang:minecraft:${minecraftVersion}"
    mappings "net.fabricmc:yarn:${minecraftVersion}+build.${yarnBuild}:v2"
    modImplementation "net.fabricmc:fabric-loader:${loaderVersion}"
    modImplementation platform("net.fabricmc.fabric-api:fabric-api-bom:${fabricApiVersion}")
    modImplementation "net.fabricmc.fabric-api:fabric-api-base"
}

processResources {
    inputs.property "version", project.version

    filesMatching('fabric.mod.json') {
        expand "version": project.version
    }

    filteringCharset = 'UTF-8'
}

java {
    withSourcesJar()
    withJavadocJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release = 21
}

indra {
    github("lucko", "fabric-permissions-api") {
        ci(true)
    }
    mitLicense()
    configurePublications {
        from components.java
        pom {
            developers {
                developer {
                    id = 'Luck'
                    url = 'https://lucko.me'
                }
            }
        }
    }
    signWithKeyFromProperties("signingKey", "signingPassword")
}
